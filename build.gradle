buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

repositories { mavenLocal() }
/*
plugins {
    id "net.minecraftforge.gradle.forge" version "2.0.2"
}
*/
// define the properties file
ext.configFile = file "build.prop"

configFile.withReader {
    // read config.  it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

allprojects {
    apply plugin: 'java'
    group = config.group_name
    version = config.mod_version
    archivesBaseName = "[${config.minecraft_version}]${config.mod_id}"
	
    minecraft {
        version = config.minecraft_version + "-" + config.forge_version
		mappings = config.mappings
        if(file('../run').exists()) {
            runDir = "../run"
        } else {
            runDir = "run"
            if (!file(runDir).exists()) file(runDir).mkdir()
        }
    }

    tasks.withType(Jar) {
        manifest {
            manifest.attributes(
                'FMLCorePlugin': 'com.bioxx.tfc2.TFCASMLoadingPlugin',
                'FMLCorePluginContainsFMLMod': 'true',
                'FMLAT': 'tfc_at.cfg'
            )
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = [ 'src/Common' , 'src/API' , 'src/ASM' ]
            exclude '**/Resources/'
        }
        resources {
            srcDirs = [ 'src/Resources' ]
            exclude '**/API'
            exclude '**/ASM'
            exclude '**/Common'
        }
    }
 }

 //Creates a compiled de-obfuscated jar
task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'deobf'
}
 //creates a jar containing only the src
/*task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
}*/

tasks.build.dependsOn('sourceJar', 'deobfJar')

processResources {
//First we just copy in the mcmod.info file
from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.prop'

        // replaces
        expand ([
            'mod_version': version,
            'minecraft_version': project.config.minecraft_version,
        ])
    }
     // copy everything else, thats we didnt do before
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.prop'
    }
}
